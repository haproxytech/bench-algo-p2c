set xlabel "\nLoad per server for each algorithm"
set ylabel "Maximum server load (lower is better)"
set border lw 2

set xrange [-1:*]
set yrange [0:500]
set boxwidth 0.6 absolute
set style histogram rowstacked
set style data histograms
#set style fill solid 1.00 border lt -1
set style fill solid 1.00 noborder
set xtics in nomirror format ""  ("" -1, "" 6, "" 13, "" 20, "" 27)
set tics scale 0.0

set linetype 10 linecolor rgb "#e0e0e0"
set style line 10 linetype 10
set grid back ls 10

set key box inside width 0.5 height 0.5 spacing 1 top right

set terminal png
set output "load-per-server.png"

# note: always set the title on the last histogram otherwise the key doubles in size.
plot \
   newhistogram "Random", "perf-smax-per-node-rnd.log"       using 5 lt 4 notitle, "" using 4 lt 3 notitle, "" using 3 lt 2 notitle, "" using 2 lt 1 notitle, \
   newhistogram "Round-robin", "perf-smax-per-node-rr.log"   using 5 lt 4 notitle, "" using 4 lt 3 notitle, "" using 3 lt 2 notitle, "" using 2 lt 1 notitle, \
   newhistogram "Power-of-Two", "perf-smax-per-node-po2.log" using 5 lt 4 notitle, "" using 4 lt 3 notitle, "" using 3 lt 2 notitle, "" using 2 lt 1 notitle, \
   newhistogram "Leastconn", "perf-smax-per-node-lc.log"     using 5 lt 4 notitle, "" using 4 lt 3 notitle, "" using 3 lt 2 notitle, "" using 2 lt 1 notitle, \
   newhistogram "External", "perf-smax-per-node-ext.log"     using 5 lt 4 title "MyTime", "" using 4 lt 3 title "Log", "" using 3 lt 2 title "Time", "" using 2 lt 1 title "User"

unset output
unset terminal
#replot
